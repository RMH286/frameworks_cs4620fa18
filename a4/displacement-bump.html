<!DOCTYPE html>
<html lang="en">
    <head>
        <title>Displacement mapping</title>
        <meta charset="utf-8">
        <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
        <style>
            body {
                font-family: Monospace;
                background-color: #707080;
                margin: 0px;
                overflow: hidden;
            }
        </style>
    </head>
    <body>
    <div id="container">
        <div id="controlBar" style="text-align: center;">
            <input type="file" id="meshInputFile" style="display: none;" onchange="loadFile(this.files[0])">
            <input type="button" id="meshInputButton" value="Select OBJ File" onclick="document.getElementById('meshInputFile').click()">
            <input type="file" id="texInputFile" style="display: none;" onchange="loadTexture(this.files[0], 'diffuseTexture')">
            <input type="button" id="texInputButton" value="Select Texture File" onclick="document.getElementById('texInputFile').click()">
            <input type="file" id="displacementInputFile" style="display: none;" onchange="loadTexture(this.files[0], 'displacementMap')">
            <input type="button" id="displacementInputButton" value="Select Displacement Map File" onclick="document.getElementById('displacementInputFile').click()">
            <label><input type="checkbox" id="showAxesCheckbox" onchange="toggleAxes(this.checked)" checked="true">Show Axes</label>
            <label><input type="checkbox" id="showWireframeCheckbox" onchange="toggleWireframe(this.checked)">Show Wireframe</label>
            <label><input type="checkbox" id="showNormalsCheckbox" onchange="toggleNormals(this.checked)" style='display:none;'></label>
            <label><input type="checkbox" id="fixLightsToCameraCheckbox" onchange="toggleFixLightsToCamera(this.checked)">Fix lights to camera</label>
            <br>
            <input type="range" id="normalLengthRange" min="0.02" max="0.5" step="0.02" value="0.3" oninput="setNormalLength(this.value)" style='display:none;'>
            Exposure:
            <input type="range" id="exposureRange" min="10.0" max="18.0" step="0.5" value="14.0" oninput="setUniformLog('exposure', this.value)">
            Roughness:
            <input type="range" id="roughnessRange" min="-16.0" max="0.0" step="0.5" value="-8.0" oninput="setUniformLog('roughness', this.value)">
            Displacement scale:
            <input type="range" id="displacementScaleRange" min="-8.0" max="0.0" step="0.25" value="-4.0" oninput="setUniformLog('displacementScale', this.value)">
            <br>
            <label><input type="radio" name="bumpdisplacement" id="bumpRadio" value='displacement' onchange="toggleDisplacementBump(this.value)" checked>Displacement mapping</label>
            <label><input type="radio" name="bumpdisplacement" id="bumpRadio" value='bump' onchange="toggleDisplacementBump(this.value)">Bump mapping</label>
            <br>
            Left click + drag to orbit. Right click + drag to pan. Scroll to zoom.
        </div>
    </div>

        <script src="js/three.min.js"></script>
        <script src="js/BufferGeometryUtils.js"></script>
        <!-- <script src="http://cdnjs.cloudflare.com/ajax/libs/three.js/r84/three.min.js"></script> -->
        <script src="js/Detector.js"></script>
        <script src="js/OrbitControls.js"></script>
        <script src="js/OBJLoader.js"></script>
<!-----------------------------------------------------------------------------
        DISPLACEMENT VERTEX SHADER
------------------------------------------------------------------------------>
        <script id="vertexShader" type="x-shader/x-vertex">
// = object.matrixWorld
// uniform mat4 modelMatrix;

// = camera.matrixWorldInverse * object.matrixWorld
// uniform mat4 modelViewMatrix;

// = camera.projectionMatrix
// uniform mat4 projectionMatrix;

// = camera.matrixWorldInverse
// uniform mat4 viewMatrix;

// = inverse transpose of modelViewMatrix
// uniform mat3 normalMatrix;

// = camera position in world space
// uniform vec3 cameraPosition;

// RenderMesh Input
// attribute vec3 position;
// attribute vec3 normal;
// attribute vec2 uv;
attribute vec3 derivU; // Non-unit vertex tangent in u direction
attribute vec3 derivV; // Non-unit vertex tangent in v direction

uniform sampler2D displacementMap;
uniform float displacementScale; // The displacement height map.

// TODO#A4: Declare any varyings here.

varying vec2 vUv;
varying vec3 vNormal; // normal at the vertex in eye space
varying vec4 vPosition; // vertex position in eye space

float delta = 0.001;

void main() {
    // TODO#A4: Compute the displaced vertex position
    // and vertex normal as described in the writeup.
    // The position should be moved along its normal by
    // the red channel of displacementMap times displacementScale.

    //gl_Position = vec4(position, 1.0); // placeholder
    vec4 new_pos = vec4(position, 1) + displacementScale * texture2D(displacementMap, uv) * vec4(normal, 0);
    gl_Position = projectionMatrix * modelViewMatrix * new_pos;

    vPosition = modelViewMatrix * new_pos;

    vec2 delta_u = vec2(delta, 0);
    vec2 delta_v = vec2(0, delta);
    vec4 dh_du = (texture2D(displacementMap, uv-delta_u) - texture2D(displacementMap, uv+delta_u)) / (2.0*delta);
    vec4 dh_dv = (texture2D(displacementMap, uv-delta_v) - texture2D(displacementMap, uv+delta_v)) / (2.0*delta);
    vec4 t_u = vec4(derivU, 0) + displacementScale * dh_du * vec4(normal, 0);
    vec4 t_v = vec4(derivV, 0) + displacementScale * dh_dv * vec4(normal, 0);
    vec3 new_normal = cross(vec3(t_u), vec3(t_v));
    vNormal = normalize((normalMatrix * new_normal).xyz);

    vUv = uv;
}
        </script>
<!-----------------------------------------------------------------------------
        DISPLACEMENT FRAGMENT SHADER
------------------------------------------------------------------------------>
        <script id="fragmentShader" type="x-shader/x-fragment">
uniform vec3 lightColors[ NUM_LIGHTS ];
uniform vec3 lightPositions[ NUM_LIGHTS ]; // in view coordinates

// Shading Information
uniform float exposure; // Multiply the final color by this.
uniform float roughness; // For Phong or Cook-Torrance.

uniform sampler2D diffuseTexture; // The diffuse color. Use texture2D to look up into this.

// TODO#A4: Declare any varyings here.

varying vec2 vUv;
varying vec3 vNormal; // fragment normal in eye space
varying vec4 vPosition; // fragment position in eye space

vec3 to_sRGB(vec3 c) { return pow(c, vec3(1.0/2.2)); }
vec3 from_sRGB(vec3 c) { return pow(c, vec3(2.2)); }

void main() {
    // TODO#A4: Implement Phong reflectance function here.

    // interpolating normals will change the length of the normal, so renormalize the normal.
    vec3 N = normalize(vNormal);
    vec3 V = normalize(-vPosition.xyz);
    
    vec3 finalColor = vec3(0.0, 0.0, 0.0);

    for (int i = 0; i < NUM_LIGHTS; i++) {
        float r = length(lightPositions[i] - vPosition.xyz);
        vec3 L = normalize(lightPositions[i] - vPosition.xyz);
        vec3 H = normalize(L + V);

        // calculate diffuse term
        vec3 Idiff = from_sRGB(texture2D(diffuseTexture, vUv).rgb) * max(dot(N, L), 0.0);

        // calculate specular term
        vec3 Ispec = vec3(1.0) * pow(max(dot(N, H), 0.0), 1.0 / roughness);

        finalColor += lightColors[i] * (Idiff + Ispec) / (r*r);
    }

    // Only shade if facing the light
    // Color the back faces an identifiable color
    if (gl_FrontFacing) {
        gl_FragColor = vec4(to_sRGB(finalColor * exposure), 1.0);
    } else {
        gl_FragColor = vec4(170.0/255.0, 160.0/255.0, 0.0, 1.0);
    }
}
        </script>

<!-----------------------------------------------------------------------------
        BUMP VERTEX SHADER
------------------------------------------------------------------------------>
        <script id="bumpVertexShader" type="x-shader/x-vertex">
attribute vec3 derivU; // Non-unit vertex tangent in u direction
attribute vec3 derivV; // Non-unit vertex tangent in v direction

// TODO#A4: Declare any varyings here.

varying vec2 vUv;
varying vec3 vNormal; // fragment normal in eye space
varying vec4 vPosition; // fragment position in eye space
varying vec3 vDerivU;
varying vec3 vDerivV;
varying mat3 vNormalMatrix;

void main() {
    // TODO#A4: Vertex position does not get displaced.
    // Pass the necessary values for normal calculation
    // as varyings to the fragment shader.

    gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1);

    vPosition = modelViewMatrix * vec4(position, 1);

    vNormal = normal;
    //vNormal = normalize((normalMatrix * normal).xyz);

    vNormalMatrix = normalMatrix;

    vDerivU = derivU;
    vDerivV = derivV;

    vUv = uv;
}
        </script>

<!-----------------------------------------------------------------------------
        BUMP FRAGMENT SHADER
------------------------------------------------------------------------------>
        <script id="bumpFragmentShader" type="x-shader/x-fragment">
uniform vec3 lightColors[ NUM_LIGHTS ];
uniform vec3 lightPositions[ NUM_LIGHTS ]; // in view coordinates

// Shading Information
uniform float exposure; // Multiply the final color by this.
uniform float roughness; // For Phong or Cook-Torrance.
uniform sampler2D diffuseTexture; // The diffuse color. Use texture2D to look up into this.

uniform float displacementScale;
uniform sampler2D displacementMap; // The displacement height map.

// TODO#A4: Declare any varyings here.

varying vec2 vUv;
varying vec3 vNormal; // fragment normal in eye space
varying vec4 vPosition; // fragment position in eye space
varying vec3 vDerivU;
varying vec3 vDerivV;
varying mat3 vNormalMatrix;

vec3 to_sRGB(vec3 c) { return pow(c, vec3(1.0/2.2)); }
vec3 from_sRGB(vec3 c) { return pow(c, vec3(2.2)); }

float delta = 0.001;

void main() {
    // TODO#A4: Compute displaced normals as in the
    // displacement vertex shader, and use them for shading.
    // Lastly, implement Phong reflectance function.

    vec2 delta_u = vec2(delta, 0);
    vec2 delta_v = vec2(0, delta);
    vec4 dh_du = (texture2D(displacementMap, vUv-delta_u) - texture2D(displacementMap, vUv+delta_u)) / (2.0*delta);
    vec4 dh_dv = (texture2D(displacementMap, vUv-delta_v) - texture2D(displacementMap, vUv+delta_v)) / (2.0*delta);
    vec4 t_u = vec4(vDerivU, 0) + displacementScale * dh_du * vec4(vNormal, 0);
    vec4 t_v = vec4(vDerivV, 0) + displacementScale * dh_dv * vec4(vNormal, 0);
    vec3 new_normal = cross(t_u.xyz, t_v.xyz);
    new_normal = normalize((vNormalMatrix * new_normal).xyz);

    // interpolating normals will change the length of the normal, so renormalize the normal.
    vec3 N = normalize(new_normal);
    vec3 V = normalize(-vPosition.xyz);
    
    vec3 finalColor = vec3(0.0, 0.0, 0.0);

    for (int i = 0; i < NUM_LIGHTS; i++) {
        float r = length(lightPositions[i] - vPosition.xyz);
        vec3 L = normalize(lightPositions[i] - vPosition.xyz);
        vec3 H = normalize(L + V);

        // calculate diffuse term
        vec3 Idiff = from_sRGB(texture2D(diffuseTexture, vUv).rgb) * max(dot(N, L), 0.0);

        // calculate specular term
        vec3 Ispec = vec3(1.0) * pow(max(dot(N, H), 0.0), 1.0 / roughness);

        finalColor += lightColors[i] * (Idiff + Ispec) / (r*r);
    }

    // Only shade if facing the light
    // Color the back faces an identifiable color
    if (gl_FrontFacing) {
        gl_FragColor = vec4(to_sRGB(finalColor * exposure), 1.0);
    } else {
        gl_FragColor = vec4(170.0/255.0, 160.0/255.0, 0.0, 1.0);
    }
}
        </script>
<!-----------------------------------------------------------------------------
        END SHADERS
------------------------------------------------------------------------------>
        <script src="js/A4Common.js"></script>
        <script>
            commonInit(true);

            init();
            animate();
            
            function init() {
                defaultTextureTarget = 'diffuseTexture';
                meshMaterial.uniforms.diffuseTexture = { type: 't', value: null };
                meshMaterial.uniforms.displacementMap = { type: 't', value: null };
                meshMaterialBump.uniforms.diffuseTexture = { type: 't', value: null };
                meshMaterialBump.uniforms.displacementMap = { type: 't', value: null };
                // texture resolutions
            
                addLight(new THREE.Vector3(50, 50, 100), new THREE.Vector3(0.8, 0.8, 0.8));
                
                document.getElementById('roughnessRange').oninput();
                document.getElementById('displacementScaleRange').oninput();
            }
        </script>
    </body>
</html>
